
define findNalu
    if $argc != 2
        help findNalu
    else
      set $SRC = *(unsigned char*)($arg0)
      set $SIZE = $arg1
      set $i = 0 
      set $encontrado = 0
      while $i < $SIZE
        if *($arg0 + $i) == 0 && *($arg0 + $i+1) == 0 && *($arg0 +$i+2) == 0 && *($arg0 +$i+3) == 1
          printf "\n Nalu encontrada en offset(%d) con direccion base:0X%x\n\n",$i,$arg0
          set $encontrado = 1
          set $i =$SIZE
        else
          set $i = $i + 1
        end
      end
      if $encontrado == 0
        print "Nalu no encontrada"
      end

  end
end
document findNalu
Dada la direccion de un bitstream y su tamaÃ±o
Indica si encuentra el codigo de inicio de nalu en su interior
Usage: findNalu src size
end


define showheader
  if $argc !=1
    help showheader
  else
    printf "at [0x%08x]:%02x %02x %02x %02x\n",$arg0,*($arg0),*($arg0 +1),*($arg0+2),*($arg0+3)
  end
end
document showheader
Imprime los primeros 4 caracteres de una direccion
Usage: showheader src
end

define naltype
  if $argc !=1
    help naltype
  else
    if *($arg0 ) == 0 && *($arg0 +1) == 0 && *($arg0 +2) == 0 && *($arg0 +3) == 1
      set $nalu_type= ( $arg0[4]  & ((1<<5)-1))
			if $nalu_type == 7
        print "SPS"
      else 
        if $nalu_type == 8
          print "PPS"
        else 
          if $nalu_type==1
            print "Imagen"
          else
            printf "Desconocido (%d)",$nalu_type
          end
        end
      end
    else
      print "No es una nalu valida"
    end
  end
end
document naltype
Dada una direccion, indicara el tipo de la nalu o mostrara un mensaje diciendo que no es una nalu valida en caso de no serlo
Usage: naltype src
end
